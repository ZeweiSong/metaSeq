#!/usr/bin/env perl
use warnings;
use strict;
use File::Basename;
use List::Util qw/min/;

sub usage {
	print <<USAGE;
usage:
	perl OAs1 fq1 <prefix> <Qual system(33|64)> <min length> <seed OA> <fragment OA> <*stLFR> <*barcode list> <*debug>
	perl OAs1 fq1,fq2 <prefix> <Qual system(33|64)> <min length> <seed OA> <fragment OA> <*stLFR> <*barcode list> <*debug>
e.g	perl OAs1 sample.1.fq,sample.2.fq clean 33 30 20 10 # PhedQ(by whole read)
		-i		input file. Use comma to specifiy paired input files
		-pfx		output prefix
		-Qsys	[33|64]	Quality system
		-minLen	[0:]	Minimal allowed read length
		-Scut	[0:50]	Threshold for seed quality
		-Qcut	[0:50]	Threshold for fragment quality
		-stLFR	[0|1|2]	off|on|only. whether turn on stLFR detection mode. If true, a barcode list file should be provided.
		-pos3	[int]	Optionally defined the last position of barcode(sometime it's 132). Default is 144(start from 0);
		-bfile		barcode list
		-debug	[0|1]	debug off or on
USAGE
	exit;
}


## load arguments
our ($fq,$pfx,$Qsys,$minLen,$Scut,$Qcut,$stlfr,$pos3,$bfile,$debug,$help);

die &usage if !defined $ARGV[0];

if($ARGV[0] =~ /^-/){
	use Getopt::Long;
	GetOptions(
	"i:s"=> \$fq,
	"pfx:s"=> \$pfx,
	"Qsys:s"=> \$Qsys,
	"minLen:s" => \$minLen,
	"Scut:s" => \$Scut,
	"Qcut:i" => \$Qcut,
	"stlfr:s"=> \$stlfr,
	"pos3:s" => \$pos3,
	"bfile:s"=> \$bfile,
	"debug:s"=> \$debug,
	"help:s" => \$help,
	);
}else{
	#Keep old argument behavior for a while
	($fq,$pfx,$Qsys,$minLen,$Scut,$Qcut,$stlfr,$pos3,$bfile,$debug) = @ARGV;
}
if($help || !defined($fq || $fq || $pfx) ){
	die &usage;
}

### Init ###
my $start_run = time();
sub openMethod {$_ = shift; return(($_ =~ /\.gz$/)?"pigz -dc $_|":"$_")}
### Create accuracy table ###
my (%P2A, %BEAD_BARCODE);
$P2A{0}= -10;
for (my $p=1;$p<=50;$p++){
	$P2A{$p} = log(1-10**(-$p/10))/log(10);
}

$minLen ||= 30;
$Scut ||= 30;
$Qcut ||= 20;
$Qsys ||= 33;
my @fqs = split /,/,$fq;
my $mode= (@fqs==2)?"PE":"SE";
$stlfr ||= 0;
$pos3  ||= 144;
### Trasnform cutoff ###
$Scut = ($Scut <= 1)?(log($Scut)/log(10)):($P2A{$Scut});
$Qcut = ($Qcut <= 1)?(log($Qcut)/log(10)):($P2A{$Qcut});

if($stlfr >0){
		&stlfr_barcode_dict($bfile);
}
our %SLOG;

### Main ###
if($mode eq "PE"){
	open F1, &openMethod($fqs[0]) or die "Input 1 error:$!\n";
	open O1,"|pigz >$pfx.clean.1.fq.gz" or die "Output 1 error:$!\n";
	open F2,&openMethod($fqs[1]) or die "Input 2 error:$!\n";
	open O2,"|pigz >$pfx.clean.2.fq.gz" or die "Output 2 error:$!\n";
	open O3,"|pigz >$pfx.clean.single.fq.gz" or die "Output 3 error:$!\n";
}else{
	open F1,&openMethod($fqs[0]) or die "Input 1 error:$!\n";
	open O1,"|pigz >$pfx.clean.fq.gz" or die "Output 1 error:$!\n";
}
open STAT,"> $pfx.clean.stat_out",or die "Open log file error:$!\n";

my %STAT;
my ($total, @remainQ, @sum_bp, @sum_oa, @sum_Q, @sum_s) = ();
my @min_bp = (1e9,1e9,1e9);
my @max_bp = (0, 0, 0);

my (@fqID,@seq,@num,@qual,@originLength,@Tlength,@Aqual,@PQ,@start,@len,@count,@avgQ) =();
while(<F1>){
	#F1 info
	(@fqID,@seq,@num,@qual,@originLength,@Tlength,@Aqual,@PQ,@start,@len,@count,@avgQ) =();
	chomp;
	($fqID[0],$seq[0],$num[0],$qual[0])= &fqRead($_,\*F1);
	if($mode eq "SE"){
		my @a = split /\t| /,$fqID[0];
		($fqID[0] = $a[0]) =~ s/\/[12]$//;
	}else{
		$total ++;
		my $l2 = <F2>;
		($fqID[1],$seq[1],$num[1],$qual[1])= &fqRead($l2,\*F2);
	}
	$total ++;

	# seek barcodes, trim & filter
	if($mode eq "PE"){
		if($stlfr==0){
			($Aqual[0],$PQ[0],$start[0],$len[0],$seq[0],$qual[0]) = &ca1_cut($Scut,$Qcut,$Qsys,$seq[0],$qual[0]);
			($Aqual[1],$PQ[1],$start[1],$len[1],$seq[1],$qual[1]) = &ca1_cut($Scut,$Qcut,$Qsys,$seq[1],$qual[1]);
		}elsif($stlfr==1){
			&stlfr_get_barcode_label($seq[1]);
			($Aqual[0],$PQ[0],$start[0],$len[0],$seq[0],$qual[0]) = &ca1_cut($Scut,$Qcut,$Qsys,$seq[0],$qual[0]);
			($Aqual[1],$PQ[1],$start[1],$len[1],$seq[1],$qual[1]) = &ca1_cut($Scut,$Qcut,$Qsys,substr($seq[1],0,100),substr($qual[1],0,100));
		}elsif($stlfr==2){
			&stlfr_get_barcode_label($seq[1]);
			($Aqual[0],$PQ[0],$start[0],$len[0]) = (1,40,0,length($seq[0]));
			($Aqual[1],$PQ[1],$start[1],$len[1]) = (1,40,0,length($seq[1]));
		}
		 # filter
		if( $len[0] >= $minLen && $Aqual[0] >= $Qcut) {
			if( $len[1] >= $minLen && $Aqual[1] >= $Qcut) {
				print O1 "$fqID[0] length=$len[0]\n$seq[0]\n$num[0]\n$qual[0]\n";
				print O2 "$fqID[1] length=$len[1]\n$seq[1]\n$num[1]\n$qual[1]\n";
				&cumulate(0,0);
				&cumulate(1,1);
			}else{
				print O3 "$fqID[0] length=$len[0]\n$seq[0]\n$num[0]\n$qual[0]\n";
				&cumulate(2,0);
			}
		}else{
			if( $len[1] >= $minLen && $Aqual[1] >= $Qcut) {
				print O3 "$fqID[1] length=$len[1]\n$seq[1]\n$num[1]\n$qual[1]\n";
				&cumulate(2,0);
			}
		}
	}else{
		($Aqual[0],$PQ[0],$start[0],$len[0],$seq[0],$qual[0]) = &ca1_cut($Scut,$Qcut,$Qsys,$seq[0],$qual[0]);
	    # filter
	    if( $len[0] >= $minLen && $Aqual[0] >= $Qcut) {
        	print O1 "$fqID[0] length=$len[0]\n$seq[0]\n$num[0]\n$qual[0]\n";
	        # stat
			&cumulate(0,0);
	    }
	}
}
close F1;
close O1;
if($mode eq "PE"){
	close F2;
	close O2;
	close O3;
}
my ($max_bp, $min_bp, $sum_bp, $sum_oa, $sum_Q, $sum_s, $remainQ) = (0, 1e9, 0, 0, 0, 0, 0);
for(my $i=0;$i<@remainQ;$i++){
	$max_bp  = ($max_bp > $max_bp[$i])?$max_bp:$max_bp[$i];
	$min_bp  = ($min_bp < $min_bp[$i])?$min_bp:$min_bp[$i];
	$sum_bp += $sum_bp[$i];
	$sum_oa += $sum_oa[$i];
	$sum_Q  += $sum_Q[$i];
	$sum_s  += $sum_s[$i];
	$remainQ+= $remainQ[$i];
}
my $avgL = sprintf("%.0f",$sum_bp / $remainQ);
my $avgOA= sprintf("%.4f",$sum_oa / $remainQ);
my $avgQ = sprintf("%.0f",$sum_Q  / $sum_bp);
my $avgS = sprintf("%.0f",$sum_s  / $remainQ);
my $rate = sprintf("%.4f",$remainQ / $total);
my $tag = basename($pfx);
#my $debugHead = ($debug)?"\tN>$n|Len<$lf|PQ<$Qf|N+Len|N+PQ|Len+PQ|HOMER":"";

print STAT "Total\tmax\tmin\tavgLen\tavgStart\tavgOA\tavgQ\tremain\trate\n";
print STAT "$total\t$max_bp\t$min_bp\t$avgL\t$avgS\t$avgOA\t$avgQ\t$remainQ\t$rate\n";
if($mode eq "PE"){

	print STAT "Read_1\tmax\tmin\tavgLen\tavgStart\tavgOA\tavgQ\n";
	printf STAT ("%d\t%d\t%d\t%d\t%d\t%.4f\t%.0f\n",
		(defined $remainQ[0])?($remainQ[0],$max_bp[0],$min_bp[0],$sum_bp[0]/$remainQ[0],
		$sum_s[0]/$remainQ[0],$sum_oa[0]/$remainQ[0],$sum_Q[0]/$remainQ[0]):(0,0,0,0,0,0,0));
	print STAT "Read_2\tmax\tmin\tavgLen\tavgStart\tavgOA\tavgQ\n";
	printf STAT ("%d\t%d\t%d\t%d\t%d\t%.4f\t%.0f\n",
		(defined $remainQ[1])?($remainQ[1],$max_bp[1],$min_bp[1],$sum_bp[1]/$remainQ[1],
		$sum_s[1]/$remainQ[1],$sum_oa[1]/$remainQ[1],$sum_Q[1]/$remainQ[1]):(0,0,0,0,0,0,0));
	print STAT "Single\tmax\tmin\tavgLen\tavgStart\tavgOA\tavgQ\n";
	printf STAT ("%d\t%d\t%d\t%d\t%d\t%.4f\t%.0f\n",
		(defined $remainQ[2])?($remainQ[2],$max_bp[2],$min_bp[2],$sum_bp[2]/$remainQ[2],
		$sum_s[2]/$remainQ[2],$sum_oa[2]/$remainQ[2],$sum_Q[2]/$remainQ[2]):(0,0,0,0,0,0,0));
}
print STAT "length\tcount\n";
foreach my $l(sort {$a<=>$b} keys %STAT){
	print STAT "$l\t$STAT{$l}\n";
}
close STAT;

if($stlfr && $debug){
	open DEBUG,"> $pfx.clean.debug",or die "Open debug file error:$!\n";
	print DEBUG "offset\tHitCode\tnum\n";
	foreach my $offset (sort {$a<=>$b} keys %{$SLOG{'hit'}}){
		foreach my $code (sort {$a<=>$b} keys %{$SLOG{'hit'}{$offset}}){
			print DEBUG "$offset\t$code\t$SLOG{'hit'}{$offset}{$code}\n";
		}
	}
	print DEBUG "\noffset\tmissCode\tnum\n";
	foreach my $offset (sort {$a<=>$b} keys %{$SLOG{'miss'}}){
		foreach my $code (sort {$a<=>$b} keys %{$SLOG{'miss'}{$offset}}){
			my $format_code = sprintf("%03d",$code);
			print DEBUG "$offset\t$format_code\t$SLOG{'miss'}{$offset}{$code}\n";
		}
	}
	close DEBUG;
}

my $end_run = time() - $start_run;
print STDERR "Running time: $end_run sec.\n";

exit;
# sub
sub fqRead {
	my $present= shift;
	my $handle = shift;
	chomp($_[0] = $present);
	chomp($_[1] = <$handle>);
	chomp($_[2] = <$handle>);
	chomp($_[3] = <$handle>);
	return(@_);
}

sub ca1_cut {
    my $Sc  = shift;
    my $cut = shift;
    my $sysQ = shift;
	my $seq = shift;
    my $q = shift;
    my ($ca, $min,$oa0, $oa1, $s,$p) = (1, 0, 1, 1, 0, 0);
    my @Q;
	my @PQ;
    # cal phrd Q
    while($p<length($q)){
        $_ = substr($q,$p,1);
        $_ = ord($_) - $sysQ;
		push @PQ, $_;
#        $_ = $P2A{$_};
        push @Q, $P2A{$_};
#       push @Q, $_;
        $p ++;
    }
    # cal first seed OA
    $p = 0;
    my @seedOA = (0);
    while($p<30){
        $seedOA[0] += $Q[$p];
        $p ++;
    }
    # choose best seed
    while($p<length($q) && $seedOA[$s]<$Sc){
        $seedOA[$p-29] = $seedOA[$p-30] + $Q[$p] - $Q[$p-30];
        $s = ($seedOA[$p-29] > $seedOA[$s])?$p-29:$s;
        $p++;
    }
    # trim
#    $ca = $seedOA[$s];
    $p = $s + 30;
    $ca = $seedOA[$s];
    while($p<length($q)){
        my $acc = $Q[$p];
        if($acc < $min){
            $oa1 = $ca + $min;
            $min = $acc;
        }else{
            $oa1 = $ca + $acc;
        }
        last if $oa1 < $cut;
        $p ++;
        $ca = $oa1;

    }
    $ca = 10**$ca;
	my $PQ =sprintf("%2d",-log(1-$ca)/log(10)*10);

	$p   = $p - $s + 1;
	$seq = substr($seq,$s,$p);
	$q   = substr($q,$s,$p);
    return($ca,$PQ,$s,$p,$seq,$q);
}

sub cumulate {
	my $n = shift;
	my $m = shift;
	$remainQ[$n]  ++;
	$STAT{$len[$m]} ++;
	$max_bp[$n] = ($max_bp[$n] > $len[$m])?$max_bp[$n]:$len[$m];
	$min_bp[$n] = ($min_bp[$n] < $len[$m])?$min_bp[$n]:$len[$m];
	$sum_bp[$n] += $len[$m];
	$sum_s[$n]  += $start[$m];
	$sum_oa[$n] += $Aqual[$m];
	$sum_Q[$n] += $PQ[$m];
}

sub stlfr_barcode_dict{
	my $bf = shift;
	my @base = ("A","T","G","C");
	open BF, "<$bf" or die $!;
	while(<BF>){
		chomp;
		my @read = split;
		$BEAD_BARCODE{'origin'}{$read[0]} = $read[1];
		for(my $i=0;$i<10;$i++){
			foreach my $snp (@base){
				my $mutate = substr($read[0],0,$i).$snp.substr($read[0],$i+1,9-$i);
				my $revMut = reverse($mutate);
				$BEAD_BARCODE{'oneSnp'}{$mutate} = $read[1];
				$BEAD_BARCODE{'RevSnp'}{$mutate} = $read[1];
			}
		}
	}
	close BF;
}

sub stlfr_get_barcode_label{
	my $seq = shift;
	my(@bcodes,@blabels);
	my %debugs = ();
	my($try,$min,$HitScore) = (0,0,0);
	my %missScore;
	my @offsets = (0,-1,1,-2,2);
	while($try<5 && $min==0){
		my $offset = $offsets[$try];
		$HitScore = 0;
		@bcodes = (substr($seq,100+$offset,10), substr($seq,116+$offset,10), substr($seq,$pos3+$offset,10));
		for(my $i=0;$i<3;$i++){
			if(defined $BEAD_BARCODE{'origin'}{$bcodes[$i]}){
				$blabels[$i] = $BEAD_BARCODE{'origin'}{$bcodes[$i]};
				$HitScore += 3*10**(2-$i);
			}elsif(defined $BEAD_BARCODE{'oneSnp'}{$bcodes[$i]}){
				$blabels[$i] = $BEAD_BARCODE{'oneSnp'}{$bcodes[$i]};
				$HitScore += 2*10**(2-$i);
			}else{
				if(defined $BEAD_BARCODE{'RevSnp'}{$bcodes[$i]}){
					$blabels[$i] = $BEAD_BARCODE{'RevSnp'}{$bcodes[$i]};
					$HitScore += 1*10**(2-$i);
				}else{
					$blabels[$i] = 0;
				}
			}
		}
		$min = min @blabels;
		if($min == 0){
			$missScore{$offset} += $HitScore;
		}
		$try ++;
	}
	if($min>0){
		$try --;
		$SLOG{'hit'}{$offsets[$try]}{sprintf("%03d",$HitScore)} ++;
	}else{
		foreach my $offset (sort keys %missScore){
			$SLOG{'miss'}{$offset}{sprintf("%03d",$missScore{$offset})} ++;
		}
	}

	my $tag = join("_",@blabels);
	$fqID[0] =~ s/\/1/\/$tag\/1/;
	$fqID[1] =~ s/\/2/\/$tag\/2/;
}
